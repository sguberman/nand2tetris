// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // x transformations
    Mux16(a=x, b=false, sel=zx, out=xa);
    Not16(in=xa, out=notxa);
    Mux16(a=xa, b=notxa, sel=nx, out=xb);
    // y transformations
    Mux16(a=y, b=false, sel=zy, out=ya);
    Not16(in=ya, out=notya);
    Mux16(a=ya, b=notya, sel=ny, out=yb);
    // f and or add
    And16(a=xb, b=yb, out=xandy);
    Add16(a=xb, b=yb, out=xplusy);
    Mux16(a=xandy, b=xplusy, sel=f, out=fout);
    // negate output
    Not16(in=fout, out=notfout);
    Mux16(a=fout, b=notfout, sel=no, out[0..7]=low8, out[8..15]=hi8, out[15]=ng, out=out);
    // zero output?
    Or8Way(in=low8, out=firsthalf);
    Or8Way(in=hi8, out=secondhalf);
    Or(a=firsthalf, b=secondhalf, out=firstorsecond);
    Not(in=firstorsecond, out=zr);
}
